apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '0.1.0'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/library-release.aar")
            groupId 'io.github.verarivotti'
            artifactId 'unifiedcircularprogress'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'unified-circular-progress'
        userOrg = 'vrivotti'
        desc = "Material circular progress bar with smooth transitions between determinate and indeterminate states."
        labels = ['android', 'progress', 'progressbar', 'circular progress', 'determinate', 'indeterminate']
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        websiteUrl = 'https://github.com/vrivotti/unified-circular-progress'
        issueTrackerUrl = 'https://github.com/vrivotti/unified-circular-progress/issues'
        vcsUrl = 'https://github.com/vrivotti/unified-circular-progress.git'
        githubRepo = 'vrivotti/unified-circular-progress'
        publish = true
        version {
            name = this.version
            desc = "unified-circular-progress ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}